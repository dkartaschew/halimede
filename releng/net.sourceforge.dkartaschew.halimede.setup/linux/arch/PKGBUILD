pkgname=halimede-ca
pkgver=1.1.0
pkgrel=1
pkgdesc="Halimede Certificate Authority"
license=(EPL)
arch=(x86_64)
url="https://halimede.sourceforge.io/"
depends=("java-environment>=8")
source=("https://sourceforge.net/projects/halimede/files/1.1.0-20191008-rc1/Halimede%20CA-linux.gtk.x86_64.tar.gz")
noextract=("Halimede%20CA-linux.gtk.x86_64.tar.gz")
sha256sums=('a1981f704a96fe0bad024662f88bfc4f81e0145e0199bca2aa13e0e132fea4f7')

prepare() {
  mkdir halimede
  bsdtar -xf "Halimede%20CA-linux.gtk.x86_64.tar.gz" -C halimede 
}

package() {
  install -D /dev/stdin "$pkgdir/usr/bin/halimede" <<END
#!/bin/bash
exec /opt/halimede/halimede "\$@"
END

  install -Dm644 halimede/halimede.desktop "$pkgdir/usr/share/applications/halimede.desktop"
  mkdir -p "$pkgdir/opt/halimede/"
  cp -aR halimede/* "$pkgdir/opt/halimede/"
}
